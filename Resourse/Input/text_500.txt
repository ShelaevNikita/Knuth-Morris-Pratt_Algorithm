/*               COPYRIGHT NOTICE

Copyright (C) 2005-2012 Mario Rodriguez Riotorto

This program is free software; you can redistribute
it and/or modify it under the terms of the
GNU General Public License as published by
the Free Software Foundation; either version 2 
of the License, or (at your option) any later version. 

This program is distributed in the hope that it
will be useful, but WITHOUT ANY WARRANTY;
without even the implied warranty of MERCHANTABILITY
or FITNESS FOR A PARTICULAR PURPOSE. See the 
GNU General Public License for more details at
http://www.gnu.org/copyleft/gpl.html
*/


/*             INTRODUCTION

This package defines some interpolation techniques.

For questions, suggestions, bugs and the like, feel free
to contact me at

mario @@@ edu DOT xunta DOT es

*/



/* Returns de input in the form of a list of pairs, ordered wrt the first */
/* coordinate. The argument must be either:                               */
/*  a) a two column matrix, p:matrix([2,4],[5,6],[9,3])                   */
/*  b) a list of pairs, p: [[2,4],[5,6],[9,3]]                            */
/*  c) a list of numbers, p: [4,6,3], in which case the abscissas will be */
/*     assigned automatically to 1, 2, 3, etc.                            */
/* This is an uxiliary function for the 'interpol' package.               */
interpol_check_input(data,funame):=
 block([n,out],
   if not listp(data) and not matrixp(data)
      then error("Argument to '",funame,"' must be a list or matrix"),
   n: length(data),
   if n<2
      then error("Argument to '",funame,"' has too few sample points")
   elseif listp(data) and every('identity,map(lambda([x], listp(x) and length(x)=2),data))
      then out: sort(data)
   elseif matrixp(data) and length(data[1]) = 2
      then out: sort(args(data))
   elseif listp(data) and every('identity,map(lambda([x], not listp(x)),data)) 
      then out: makelist([i,data[i]],i,1,n)
      else error("Error in arguments to '",funame,"' function"),
   /* controlling duplicated x's */
   for i:2 thru n do
      if out[i-1][1] = out[i][1]
         then error("Duplicated abscissas are not allowed"),
   out )$



/* Lagrangian interpolation. The argument must be either:                      */
/*  a) a two column matrix, p:matrix([2,4],[5,6],[9,3])                        */
/*  b) a list of pairs, p: [[2,4],[5,6],[9,3]]                                 */
/*  c) a list of numbers, p: [4,6,3], in which case the abscissas will be      */
/*     assigned automatically to 1, 2, 3, etc.                                 */
/* In cases a) and b) the pairs are ordered wrt the 1st. coordinate before any */
/* computation is made. Option:                                                */
/*   'varname='x: the name of the independent variable                         */
/* Sample session:                                                             */
/* load(interpol);                                                             */
/* p:[[7,2],[8,2],[1,5],[3,2],[6,7]]$                                          */
/* lagrange(p);                                                                */
/* f(x):=''%;                                                                  */
/* map(f,[2.3,5/7,%pi]);                                                       */
/* load(draw)$;                                                                */
/* draw2d(                                                                     */
/*    explicit(f(x),x,0,9),                                                    */
/*    point_size = 3,                                                          */
/*    points(p)) $                                                             */
lagrange(tab,[select]) := block([n,sum:0,prod,options,defaults,ratprint:false,tab2],
   tab2: interpol_check_input(tab,"lagrange"),
   options:  ['varname],
   defaults: ['x],
   for i in select do(
      aux: ?position(lhs(i),options),
      if numberp(aux) and aux <= length(options) and aux >= 1
        then defaults[aux]: rhs(i)),
   if not symbolp(defaults[1])
      then error("Option 'varname' is not correct"),

   /* constructing the interpolating polynomial */
   n: length(tab2),
   for i:1 thru n do(
      prod: 1,
      for k:1 thru n do
         if k#i then prod: prod * (defaults[1]-tab2[k][1]) / (tab2[i][1]-tab2[k][1]),
      sum: sum + prod * tab2[i][2] ),
   sum )$



/* Characteristic function for intervals. Returns true iif  */
/* z belongs to [l1, l2). This is an auxiliary function to  */
/* be called from linearinterpol and cspline                */
charfun2(z,l1,l2):= charfun(l1 <= z and z < l2)$



/* Linear interpolation. The argument must be either:                          */
/*  a) a two column matrix, p:matrix([2,4],[5,6],[9,3])                        */
/*  b) a list of pairs, p: [[2,4],[5,6],[9,3]]                                 */
/*  c) a list of numbers, p: [4,6,3], in which case the abscissas will be      */
/*     assigned automatically to 1, 2, 3, etc.                                 */
/* In cases a) and b) the pairs are ordered wrt the 1st. coordinate before any */
/* computation is made. Option:                                                */
/*   'varname='x: the name of the independent variable                         */
/* Sample session:                                                             */
/* load(interpol);                                                             */
/* p:[[7,2],[8,2],[1,5],[3,2],[6,7]]$                                          */
/* linearinterpol(p);                                                          */
/* f(x):=''%;                                                                  */
/* map(f,[2.3,5/7,%pi]);                                                       */
/* load(draw)$;                                                                */
/* draw2d(                                                                     */
/*    explicit(f(x),x,0,9),                                                    */
/*    point_size = 3,                                                          */
/*    points(p)) $                                                             */
linearinterpol(tab,[select]) := block([n,s:0,a,b,options, defaults,ratprint:false,tab2],
   tab2: interpol_check_input(tab,"linearinterpol"),
   options:  ['varname],
   defaults: ['x],
   for i in select do(
      aux: ?position(lhs(i),options),
      if numberp(aux) and aux <= length(options) and aux >= 1
        then defaults[aux]: rhs(i)),
   if not symbolp(defaults[1])
      then error("Option 'varname' is not correct"),

   /* constructing the interpolating polynomial */
   n: length(tab2),
   if n=2 /* case of two points */
      then s: tab2[2][2] + (tab2[2][2]-tab2[1][2]) *
                          (defaults[1]-tab2[2][1]) /
                          (tab2[2][1]-tab2[1][1])
      else for i:2 thru n do(
               if i=2
                  then (a: 'minf, b: tab2[i][1])
                  else if i=n
                          then (a: tab2[i-1][1], b: 'inf)
                          else (a: tab2[i-1][1], b: tab2[i][1]),
               s: s + apply('charfun2,[defaults[1], a, b]) *
                      expand( tab2[i][2] + (tab2[i][2]-tab2[i-1][2]) *
                                    (defaults[1]-tab2[i][1]) /
                                    (tab2[i][1]-tab2[i-1][1]) )   ),
   s )$



/* Cubic splines interpolation. The argument must be either:                           */
/*  a) a two column matrix, p:matrix([2,4],[5,6],[9,3])                                */
/*  b) a list of pairs, p: [[2,4],[5,6],[9,3]]                                         */
/*  c) a list of numbers, p: [4,6,3], in which case the abscissas will be              */
/*     assigned automatically to 1, 2, 3, etc.                                         */
/* In cases a) and b) the pairs are ordered wrt the 1st. coordinate before any         */
/* computation is made. Options:                                                       */
/*   'd1='unknown: 1st derivative at x_1; if it is 'unknown, the second derivative     */
/*         at x_1 is made equal to 0 (natural cubic spline); if it is equal to a       */
/*         number, the second derivative is estimated based on this number             */
/*   'd2='unknown: 1st derivative at x_n; if it is 'unknown, the second derivative     */
/*         at x_n is made equal to 0 (natural cubic spline); if it is equal to a       */
/*         number, the second derivative is estimated based on this number             */
/*   'varname='x: the name of the independent variable                                 */
/* Reference: this algorithm is based on 'Numerical Recipes in C', section 3.3         */
/* Sample session:                                                                     */
/* load(interpol);                                                                     */
/* p:[[7,2],[8,2],[1,5],[3,2],[6,7]]$                                                  */
/* cspline(p); ==> natural cubic spline (second derivatives are zero in both extremes) */
/* f(x):=''%;                                                                          */
/* map(f,[2.3,5/7,%pi]);                                                               */
/* load(draw)$;                                                                        */
/* draw2d(                                                                             */
/*    explicit(f(x),x,0,9),                                                            */
/*    point_size = 3,                                                                  */
/*    points(p)) $                                                                     */
/* cspline(p,d1=0,dn=0);                                                               */
/* g(x):=''%;                                                                          */
/* draw2d(                                                                             */
/*    explicit(g(x),x,0,9),                                                            */
/*    point_size = 3,                                                                  */
/*    points(p)) $                                                                     */
cspline(tab,[select]):= block([options, defaults, n, aux, y2, u, sig, p,
                               qn, un, a, b, s:0, aj, bj, cj, dj, ratprint:false,tab2],
   tab2: interpol_check_input(tab,"cspline"),
   options:  ['d1, 'dn, 'varname],
   defaults: ['unknown, 'unknown, 'x],
   for i in select do(
      aux: ?position(lhs(i),options),
      if numberp(aux) and aux <= length(options) and aux >= 1
        then defaults[aux]: rhs(i)),
   if not numberp(defaults[1]) and  defaults[1] # 'unknown
      then error("Option 'd1' is not correct"),
   if not numberp(defaults[2]) and  defaults[2] # 'unknown
      then error("Option 'dn' is not correct"),
   if not symbolp(defaults[3])
      then error("Option 'varname' is not correct"),

   /* if tab2 contains only two points, linear interpolation */
   n: length(tab2),
   if n=2 /* case of two points */
      then return(ratsimp( tab2[2][2] + (tab2[2][2]-tab2[1][2]) *
                                       (defaults[3]-tab2[2][1]) /
                                       (tab2[2][1]-tab2[1][1]))),


   /* constructing the interpolating polynomial */
   y2: makelist(0,i,1,n),
   u: makelist(0,i,1,n-1),

   /* controlling the lower boundary condition */
   if /*d1*/ defaults[1] = 'unknown
      then (y2[1]: 0,
            u[1]: 0)
      else (y2[1]: -1/2,
            u[1]: 3 / (tab2[2][1]-tab2[1][1]) *
                      ((tab2[2][2] - tab2[1][2])/(tab2[2][1] - tab2[1][1]) - defaults[1]) ),

   /* decomposition loop of the triangular algorithm */
   for i:2 thru n-1 do (
      sig: (tab2[i][1] - tab2[i-1][1]) / (tab2[i+1][1] - tab2[i-1][1]),
      p: sig * y2[i-1] + 2,
      y2[i]: (sig - 1) / p,
      u[i]: (tab2[i+1][2] - tab2[i][2]) /(tab2[i+1][1] - tab2[i][1]) -
            (tab2[i][2] - tab2[i-1][2]) /(tab2[i][1] - tab2[i-1][1]),
      u[i]: (6 * u[i] / (tab2[i+1][1] - tab2[i-1][1]) - sig * u[i-1]) / p ) ,

   /* controlling the upper boundary condition */
   if /*dn*/ defaults[2] = 'unknown
      then (qn: 0,
            un: 0)
      else (qn: 1/2,
            un: 3 / (tab2[n][1] - tab2[n-1][1]) *
                (defaults[2] - (tab2[n][2] - tab2[n-1][2]) / (tab2[n][1] - tab2[n-1][1]))),
   y2[n]: (un - qn * u[n-1]) / (qn * y2[n-1] + 1),

   /* backsubstitution loop of the tridiagonal algorithm */
   for k: n-1 thru 1 step -1 do
      y2[k]: y2[k] * y2[k+1] + u[k],

   /* constructing the cubic splines */
   for j:2 thru n do (
      if j=2
          then (a: 'minf, b: tab2[j][1] )
          else if j=n
                  then (a: tab2[j-1][1], b: 'inf)
                  else (a: tab2[j-1][1], b: tab2[j][1]),
      /* in the following sentences, defaults[3] is variable's name */
      aux: (tab2[j][1] - tab2[j-1][1]),
      aj: (tab2[j][1] - defaults[3]) / aux,
      bj: (defaults[3] - tab2[j-1][1]) / aux,
      aux: aux * aux /6,
      cj: (aj^3 - aj) * aux,
      dj: (bj^3 - bj) * aux,

      s: s + charfun2(defaults[3], a, b) *
             expand(aj * tab2[j-1][2] + bj * tab2[j][2] + cj * y2[j-1] + dj * y2[j])  ),
   s )$



/* Rational interpolation, with interpolating function of the form             */
/*               r                                                             */
/*           p  x  + ... + p  x + p                                            */
/*            r             1      0                                           */
/*    R(x) = ------------------------                                          */
/*               m                                                             */
/*           q  x  + ... + q  x + q                                            */
/*            m             1      0                                           */
/* The 2nd. argument r is the degree of the numerator (r < sample size). The   */
/* degree of the denominator is calculated as m: sample_size - r - 1.          */
/* The 1st. argument must be either:                                           */
/*  a) a two column matrix, p:matrix([2,4],[5,6],[9,3])                        */
/*  b) a list of pairs, p: [[2,4],[5,6],[9,3]]                                 */
/*  c) a list of numbers, p: [4,6,3], in which case the abscissas will be      */
/*     assigned automatically to 1, 2, 3, etc.                                 */
/* In cases a) and b) the pairs are ordered wrt the 1st. coordinate before any */
/* computation is made. Option:                                                */
/*   'varname='x: the name of the independent variable                         */
/* Sample session:                                                             */
/* load(interpol)$                                                             */
/* load(draw)$                                                                 */
/* p: [[7.2, 2.5], [8.5, 2.1], [1.6, 5.1], [3.4, 2.4], [6.7, 7.9]]$            */
/* for k:0 thru length(p)-1 do                                                 */
/*   draw2d(explicit(ratinterpol(p,k),x,0,9),                                  */
/*          point_size = 3,                                                    */
/*          points(p),                                                         */
/*          title = concat("Degree of numerator = ",k),                        */
/*          yrange=[0,10])$                                                    */
ratinterpol(tab,r,[select]) :=
  block([n,m,coef,unk,sol,lovtab,lov,options,defaults,ratprint:false,tab2],
    tab2: interpol_check_input(tab,"ratinterpol"),
    options:  ['varname],
    defaults: ['x],
    for i in select do(
       aux: ?position(lhs(i),options),
       if numberp(aux) and aux <= length(options) and aux >= 1
         then defaults[aux]: rhs(i)),
    if not symbolp(defaults[1])
       then error("Option 'varname' is not correct"),

    /* constructing the interpolating rational function */
    n: length(tab2),
    if not integerp(r) or r > n-1 or r < 0
      then error("Degree of numerator must be a positive integer less than sample size"),
    m: n - r - 1, /* degree of denominator */

    /* coef is the matrix of an homogeneous linear system */
    coef: apply(matrix,
                makelist(block([x,y],
                           [x,y]: p,
                           append([1],
                                  makelist(x^k,k,1,r),
                                  [-y],
                                  makelist(-y*x^k,k,1,m))),
                         p, tab2)),
    unk: makelist(gensym(), k, r+m+2),
    sol: map(last, linsolve(flatten(args(coef.unk)),unk)),
    lovtab : listofvars(tab2),
    lov: listofvars(sol),
    lov: listify(setdifference(setify(lov),setify(lovtab))),
    sol: subst(map(lambda([z1,z2], z1=z2), lov, makelist(1, k, length(lov))), sol),
    makelist(sol[k],k,1,r+1) . makelist(defaults[1]^k,k,0,r) / 
        makelist(sol[k],k,r+2,r+2+m) . makelist(defaults[1]^k,k,0,m) )$

;;
;;                                  ~*~  PRINTF  ~*~
;;
;; Formatted printing to character streams
;;
;; Copyright     : 2005-2015 Volker van Nek
;; Licence       : GPL2
;;
;; Test file     : rtestprintf.mac (lots of examples)
;; Documentation : stringproc.texi 
;;

#|

`$printf' is an interface to the Lisp function `format'.
`$printf' provides a directive ~w,d,e,x,o,p@h to process bigfloat numbers.
Arguments which are passed to d,f,e,g or h-directives are converted to that type.

Before passing to `format' ctrls and args are both visited and modified:
E.g. `prepare-ctrls' replaces the directive ~w,d,e,x,o,p@h by ~@a 
and `prepare-args' replaces the corresponding bigfloat argument by a string. 
As a consequence of this no arg can be used twice. The goto-directive ~* is lost.

In GCL/utf-8 builds mincol specifications do not work correctly 
if non-us-ascii characters are used as arguments to ~s and ~a directives.

|#

(in-package :maxima)

;; This is an implementation of the formerly src/plot.lisp/$sprint 
;; as a simple wrapper for printf(true,"~@{~a ~}",x,y,z,...)
;; keeping the original return value: the first argument resp. false
;;
(defun $sprint (&rest args)
  (loop for v in args do
    ($printf t "~a " v))
  (car args) )


(defvar $stderr)
(defvar $stdout)
(defvar $stdin)

;; Make stdout, stdin and stderr user-accessible
;;
(setq $stderr *error-output*
      $stdout *standard-output*
      $stdin  *standard-input* )


(defun $printf (stream ctrls &rest args)
  (cond 
    ((and (not (member stream '(t nil))) (not (streamp stream)))
      (gf-merror (intl:gettext 
        "`printf': first argument must be `true', `false' or a stream." )))
    ((not (stringp ctrls))
      (gf-merror (intl:gettext "`printf': second argument must be a string.")) ))
  (setq args (prepare-args ctrls args nil))
  (let ((body (mapcar #'(lambda (x) (if (listp x) `(quote ,x) x)) args)))
    (setq ctrls (prepare-ctrls ctrls))
    (eval `(format ,stream ,ctrls ,@body)) ))


(defun prepare-args (ctrls todo done &optional (loop nil))
  (let ((start 0) pos1 pos2 pos1a
         params spec subctrls 
        (skip 0) (loops nil) (index 0) )
    (do ((arg (car todo)))
        ((null todo)
          (setq pos1 (search "~" ctrls :start2 start))
          (when pos1
            (setq pos2 (spec-position ctrls pos1)
                  spec (subseq ctrls (1- pos2) pos2) ))
          (if (and (zerop skip) (or (not pos1) (search spec "^{}[]<>%;&~tpTP
")));; newline possible spec
            (reverse done)
            (gf-merror (intl:gettext "`printf': arguments exhausted.")) ))
      (prog ()
       tag1
;; recognize the directive:
        (setq pos1 (search "~" ctrls :start2 start))
        (unless pos1
          (cond 
            (loop
              (setq start 0 
                    pos1 (search "~" ctrls :start2 start) ))
            (t
              (push arg done)
              (go tag3) )))
        (setq pos2 (setq pos1a (spec-position ctrls pos1))
              spec (subseq ctrls (1- pos2) pos2) )
        (when (search spec "}]>;%&t~") (setq start pos2) (go tag1))
        (setq params (subseq ctrls (1+ pos1) (1- pos2)))
;;
;; pre-test for ~nr, ~vr, ~#r :
;; check if radix is legal (Maxima 5.14 gcl segfaults when radix is 1)
        (when (and (string-equal spec "r") (string/= "" params))
          (let ((ch (subseq params 0 1)) (n "") (len (length params)) radix)
            (when (or ($digitcharp ch) (search ch "v#V")) ;; stringproc.lisp/$digitcharp 
              (do ((p 1 (1+ p)))
                  ((or (= p len) (search ch ",@:v#V")))
                (setq n (concatenate 'string n ch)
                      ch (subseq params p (1+ p)) ))
              (setq radix
                (cond
                  ((string= ch ",") 10.)
                  ((string-equal ch "v") arg)
                  ((string= ch "#") (length todo))
                  ((or (string= ch "@") (string= ch ":")) (parse-integer n))
                  (t (parse-integer (concatenate 'string n ch))) ))
              (when (or (< radix 2.) (> radix 36.))
                (gf-merror (intl:gettext "`printf': illegal radix in r-directive: ~m") radix)) )))
;;
;; handle some special directives:
        (cond
;; ~v,v,v<spec>, spec=ABDEFGORSTX<~&%$   (# needs no special care; ~v[ not supported, see below)
          ((search spec "abdefgorstx<~&%$" :test #'string-equal)
            (when (> (setq skip (count-v params)) 0)
              (do () ((zerop skip))
                (push (if (stringp arg) (character arg) arg) done)
                (setq arg (car (setq todo (cdr todo))))
                (incf index)
                (decf skip) )
              (push (prepare-arg params spec arg) done)
              (go tag2) ))
;; ~v,#,vH
          ((string-equal spec "h")
            (when (check-v# params)
              (let ((prms (split-at-comma params))
                     prm (new-params "") )
                (dolist (p prms)
                  (setq prm
                    (cond
                      ((string-equal "v" p)
                        (prog1
                          ($sconcat arg)
                          (setq arg (car (setq todo (cdr todo))))
                          (incf index) ))
                      ((string= "#" p) ($sconcat (length todo)))
                      (t p) ))
                  (setq new-params (concatenate 'string new-params prm ",")) )
                (push (prepare-arg new-params spec arg) done)
                (go tag2) )))
;; ~@[, ~#[, ~n[
          ((string= "[" spec)
            (cond
              ((string= "" params)) ;; don't check another condition
              ((or (and (string= "@" params) arg) ;; if arg is not nil, arg is not consumed
                   (string= "#" params) ) 
                (setq start pos2)
                (go tag1) )
              ((or (string= "v" params)
                   (every #'digit-char-p (coerce params 'list)))
                (gf-merror (intl:gettext "`printf': not supported directive ~~~m[") params) ))) ;; 0- vs. 1-based indexing
;; ~?
          ((string= "?" spec)
            (cond
              ((string= "" params)
                (let ((ind-ctrls arg)) ;; arg is a string
                  (push arg done)
                  (setq arg (car (setq todo (cdr todo))))
                  (push (prepare-args ind-ctrls (cdr arg) nil nil) done)
                  (incf index)
                  (go tag2) ))
              ((string= "@" params)...